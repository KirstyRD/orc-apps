package org.ietr.trackImg;

import org.ietr.trackImg.DisplayNative.*;
import  std.util.Math.*;
import  std.stdio.Source.*;
//import  std.stdio.Display.*;
import org.ietr.trackImg.DisplayFunctions.*;


actor Display()
	uint(size=8)  Red,
	uint(size=8)  Green,
	uint(size=8)  Blue,
	uint(size=8)  RectCoords,
	int PictSize
		==>
		:

	uint(size=8)  pictureBufferR[WIDTH * HEIGHT];
	uint(size=8)  pictureBufferG[WIDTH * HEIGHT];
	uint(size=8)  pictureBufferB[WIDTH * HEIGHT];
	int (size=32) pictureSizeInMb;
	int (size=32) nbBlockGot;
	int (size=32) nbFrameDecoded;
	uint(size=16) cropPicWthLuma;
	uint(size=16) cropPicHghtLuma;
	
	uint(size=14) xMin;
	uint(size=14) xMax;
	uint(size=14) yMin;
	uint(size=14) yMax;
	
	int Rect_x;
	int Rect_y;
	int Rect_w;
	int Rect_h;

	getPictureSize: action RectCoords:[x,y,w,h], PictSize:[width, height] ==>
	do
		displayRGB_init(width, height, width, height);
		Rect_x := x;
		Rect_y := y;
		Rect_w := w;
		Rect_h := h;
		pictureSizeInMb   := width * height / MB_SIZE_IN_PIX;
		xMin := 0;
		xMax := width - 1;
		yMin := 0;
		yMax := height - 1;
		cropPicWthLuma  := xMax - xMin + 1;
		cropPicHghtLuma := yMax - yMin + 1;
		nbBlockGot := 0;
	end

	getBlocks: action Red :[red] repeat MB_SIZE_IN_PIX, Green :[green] repeat MB_SIZE_IN_PIX, Blue :[blue] repeat MB_SIZE_IN_PIX ==>
	guard
		nbBlockGot < pictureSizeInMb
	do
		
		foreach int i in 0 .. MB_SIZE_IN_PIX - 1 do
			pictureBufferR[i + MB_SIZE_IN_PIX * nbBlockGot] := red[i];
			pictureBufferG[i + MB_SIZE_IN_PIX * nbBlockGot] := green[i];
			pictureBufferB[i + MB_SIZE_IN_PIX * nbBlockGot] := blue[i];
		end
		nbBlockGot := nbBlockGot + 1;
	end

	displayPicture: action ==>
	guard
		nbBlockGot = pictureSizeInMb
	do
		fpsPrintNewPicDecoded();
		if(DISP_ENABLE != 0) then
			displayRGB_displayPicture(pictureBufferR, pictureBufferG, pictureBufferB, cropPicWthLuma, cropPicHghtLuma);
			displayTrackRect( Rect_x, Rect_y, Rect_w, Rect_h);
		end
		nbFrameDecoded := nbFrameDecoded + 1;
	end
	
	schedule fsm GetPictureSize:
		GetPictureSize    	(getPictureSize   	)--> GetBlocks;
		GetBlocks      		(getBlocks  		)--> GetBlocks;
		GetBlocks      		(displayPicture   	)--> GetPictureSize;
	end
	
end