// Authors: Eduardo Juarez, Wei Jianguo and Ren Rong <eduardo.juarez@upm.es> of UPM (Spain) 
// modified by: Damien de Saint Jorre <damien.desaintjorre@epfl.ch>

package org.ietr.mpegh.part2.main.IT;

import devel.org.sc29.wg11.mpegh.part2.common.CommonConstantHEVC.*;
import org.ietr.mpegh.part2.main.IT.TransformFunctions.*;

actor IT_yuv()
	int(size=8)  Size,                                          
	int(size=16) Coeff
		==>                                    
	int(size=16)  Block_y,
	int(size=16)  Block_u, 
	int(size=16)  Block_v  
		:
	
 	uint(size=2) cIdx := 0;
 	uint(size=3) blk4x4Idx := 0;
 	uint(size=9) numPartToSend;
 	uint(size=9) partIdx := 0;
 
 	int(size=16) lumaBlk[32*32];
 	int(size=16) chrBlk_u[16*16];
 	int(size=16) chrBlk_v[16*16];

	procedure incIdx(int tuSize)
	begin
		if(tuSize = 4 and cIdx = 0) then
			blk4x4Idx := blk4x4Idx + 1;
			if(blk4x4Idx = 4) then
				blk4x4Idx := 0;
				cIdx := cIdx + 1;
			end
		else
			cIdx := cIdx + 1;
		end
	end

	getCoeff.isLuma.is4x4: action
		Size:[sizeOfTU, RCskip, isDST, transfSkipFlag, slcQp, rcCIdx, qpOff],
		Coeff:[coeff] repeat 4*4
			==>
	guard
		cIdx = 0 and sizeOfTU = 4 and RCskip = 0
	var
		int(size=8) tuSize = 4,
		uint(size=6) offset = 16 * blk4x4Idx
	do
		if(transfSkipFlag = 1) then
		    transform_4x4_4_skip_zscan(lumaBlk, coeff, 4, offset);
		elsif(isDST = 1) then
			transform_4x4_luma_zscan(lumaBlk, coeff, 2, offset);
		else
			transform_4x4_zscan(lumaBlk, coeff, 2, offset);
		end

		incIdx(tuSize);
		numPartToSend := 4;
	end
	
	getCoeff.isChromaU.is4x4: action
		Size:[sizeOfTU, RCskip, isDST, transfSkipFlag, slcQp, rcCIdx, qpOff],
		Coeff:[coeff] repeat 4*4
			==>
	guard
		cIdx = 1 and sizeOfTU = 4 and RCskip = 0
	var
		int(size=8) tuSize = 4
	do
		if(transfSkipFlag = 1) then
			transform_4x4_2_skip_zscan(chrBlk_u, coeff, 4, 0);
		else
			transform_4x4_zscan(chrBlk_u, coeff, 1, 0);
		end

		incIdx(tuSize);
		numPartToSend := 4;
	end
	
	getCoeff.isChromaV.is4x4: action
		Size:[sizeOfTU, RCskip, isDST, transfSkipFlag, slcQp, rcCIdx, qpOff],
		Coeff:[coeff] repeat 4*4
			==>
	guard
		cIdx = 2 and sizeOfTU = 4 and RCskip = 0
	var
		int(size=8) tuSize = 4
	do
		if(transfSkipFlag = 1) then
			transform_4x4_2_skip_zscan(chrBlk_v, coeff, 4, 0);
		else
			transform_4x4_zscan(chrBlk_v, coeff, 1, 0);
		end

		incIdx(tuSize);
		numPartToSend := 4;
	end

	getCoeff.is4x4.skip: action
		Size:[sizeOfTU, RCskip, isDST, transfSkipFlag, slcQp, rcCIdx, qpOff]
			==>
	guard
		sizeOfTU = 4 and RCskip != 0
	var
		int(size=8) tuSize = 4
	do
		if(cIdx = 0) then
			foreach int i in 0 .. 15
			do
				lumaBlk[16*blk4x4Idx +i] := 0;
			end
		elsif(cIdx = 1) then
			chrBlk_u := [0: for int i in 0 .. 15];
		else
			chrBlk_v := [0: for int i in 0 .. 15];
		end
		incIdx(tuSize);
		numPartToSend := 4;
	end

	getCoeff.isLuma.is8x8: action
		Size:[sizeOfTU, RCskip, isDST, transfSkipFlag, slcQp, rcCIdx, qpOff],
		Coeff:[coeff] repeat 8*8
			==>
	guard
		cIdx = 0 and sizeOfTU = 8 and RCskip = 0
	var
		int(size=8) tuSize = 8
	do
		if(transfSkipFlag = 1) then
			transform_8x8_4_skip_zscan(lumaBlk, coeff, 4, 0);
		else
			transform_8x8_zscan(lumaBlk, coeff, 4, 0);
		end

		incIdx(tuSize);
	end
	
	getCoeff.isChromaU.is8x8: action
		Size:[sizeOfTU, RCskip, isDST, transfSkipFlag, slcQp, rcCIdx, qpOff],
		Coeff:[coeff] repeat 8*8
			==>
	guard
		cIdx = 1 and sizeOfTU = 8 and RCskip = 0
	var
		int(size=8) tuSize = 8
	do
		if(transfSkipFlag = 1) then
			transform_8x8_2_skip_zscan(chrBlk_u, coeff, 2, 0);
		else
			transform_8x8_zscan(chrBlk_u, coeff, 2, 0);
		end

		incIdx(tuSize);
	end
	
	getCoeff.isChromaV.is8x8: action
		Size:[sizeOfTU, RCskip, isDST, transfSkipFlag, slcQp, rcCIdx, qpOff],
		Coeff:[coeff] repeat 8*8
			==>
	guard
		cIdx = 2 and sizeOfTU = 8 and RCskip = 0
	var
		int(size=8) tuSize = 8
	do
		if(transfSkipFlag = 1) then
			transform_8x8_2_skip_zscan(chrBlk_v, coeff, 1, 0);
		else
			transform_8x8_zscan(chrBlk_v, coeff, 1, 0);
		end

		incIdx(tuSize);
	end

	getCoeff.is8x8.skip: action
		Size:[sizeOfTU, RCskip, isDST, transfSkipFlag, slcQp, rcCIdx, qpOff]
			==>
	guard
		sizeOfTU = 8 and RCskip != 0
	var
		int(size=8) tuSize = 8
	do
		if(cIdx = 0) then
			lumaBlk := [0: for int i in 0 .. 63];
		elsif(cIdx = 1) then
			chrBlk_u := [0: for int i in 0 .. 63];
		else
			chrBlk_v := [0: for int i in 0 .. 63];
		end
		incIdx(tuSize);
	end

	getCoeff.isLuma.is16x16: action
		Size:[sizeOfTU, RCskip, isDST, transfSkipFlag, slcQp, rcCIdx, qpOff],
		Coeff:[coeff] repeat 16*16
			==>
	guard
		cIdx = 0 and sizeOfTU = 16 and RCskip = 0
	var
		int(size=8) tuSize = 16
	do
		if(transfSkipFlag = 1) then
			transform_16x16_4_skip_zscan(lumaBlk, coeff, 4, 0);
		else
			transform_16x16_zscan(lumaBlk, coeff, 4, 0);
		end

		if(cIdx = 0) then
			numPartToSend := 16;
		end
		incIdx(tuSize);
	end
	
	getCoeff.isChromaU.is16x16: action
		Size:[sizeOfTU, RCskip, isDST, transfSkipFlag, slcQp, rcCIdx, qpOff],
		Coeff:[coeff] repeat 16*16
			==>
	guard
		cIdx = 1 and sizeOfTU = 16 and RCskip = 0
	var
		int(size=8) tuSize = 16
	do
		if(transfSkipFlag = 1) then
			transform_16x16_2_skip_zscan(chrBlk_u, coeff, 2, 0);
		else
			transform_16x16_zscan(chrBlk_u, coeff, 2, 0);
		end

		if(cIdx = 0) then
			numPartToSend := 16;
		end
		incIdx(tuSize);
	end
	
	getCoeff.isChromaV.is16x16: action
		Size:[sizeOfTU, RCskip, isDST, transfSkipFlag, slcQp, rcCIdx, qpOff],
		Coeff:[coeff] repeat 16*16
			==>
	guard
		cIdx = 2 and sizeOfTU = 16 and RCskip = 0
	var
		int(size=8) tuSize = 16
	do
		if(transfSkipFlag = 1) then
			transform_16x16_2_skip_zscan(chrBlk_v, coeff, 2, 0);
		else
			transform_16x16_zscan(chrBlk_v, coeff, 2, 0);
		end

		if(cIdx = 0) then
			numPartToSend := 16;
		end
		incIdx(tuSize);
	end

	getCoeff.is16x16.skip: action
		Size:[sizeOfTU, RCskip, isDST, transfSkipFlag, slcQp, rcCIdx, qpOff]
			==>
	guard
		sizeOfTU = 16 and RCskip != 0
	var
		int(size=8) tuSize = 16
	do
		if(cIdx = 0) then
			numPartToSend := 16;
			lumaBlk := [0: for int i in 0 .. 255];
		elsif(cIdx = 1) then
			chrBlk_u := [0: for int i in 0 .. 255];
		else
			chrBlk_v := [0: for int i in 0 .. 255];
		end
		incIdx(tuSize);
	end

	/* cIdx will be 0 */
	getCoeff.is32x32: action
		Size:[sizeOfTU, RCskip, isDST, transfSkipFlag, slcQp, rcCIdx, qpOff],
		Coeff:[coeff] repeat 32*32
			==>
	guard
		sizeOfTU = 32 and RCskip = 0
	var
		int(size=8) tuSize = 32
	do
		if(transfSkipFlag = 1) then
			transform_32x32_skip_zscan(lumaBlk, coeff, 4, 0);
		else
			transform_32x32_zscan(lumaBlk, coeff, 4, 0);
		end
		
		numPartToSend := 64;
		incIdx(tuSize);
	end

	getCoeff.is32x32.skip: action
		Size:[sizeOfTU, RCskip, isDST, transfSkipFlag, slcQp, rcCIdx, qpOff]
			==>
	guard
		sizeOfTU = 32 and RCskip != 0
	do
		lumaBlk := [0: for int i in 0 .. 1023];
		numPartToSend := 64;
		incIdx(32);
	end

	getCoeff_is64x64: action
		Size:[param] repeat 7*3
			==>
		Block_y:[[0: for int i in 0 .. 4095]] repeat 4096, 
		Block_u:[[0: for int i in 0 .. 1023]] repeat 1024, 
		Block_v:[[0: for int i in 0 .. 1023]] repeat 1024
	guard
		param[0] = 64 //sizeOfTU = 64
	end

	sendToken.launch: action ==>
	guard
		cIdx = 3
	do
		cIdx    := 0;
		partIdx := 0;
	end

	sendToken.skip: action ==>
	guard
		cIdx < 3
	end

	sendBlock.launch: action 
			==> 
		Block_y:[[lumaBlk[16*idx + i]: for int i in 0 .. 63]] repeat 64, 
		Block_u:[[chrBlk_u[4*idx + i]: for int i in 0 .. 15]] repeat 16, 
		Block_v:[[chrBlk_v[4*idx + i]: for int i in 0 .. 15]] repeat 16
	guard
		partIdx < numPartToSend
	var
		uint(size=9) idx = partIdx
	do
		partIdx := partIdx + 4;
	end

	sendBlock.done: action ==>
	guard
		partIdx = numPartToSend
	do
		partIdx := 0;
	end

	schedule fsm GetCoeff:
		GetCoeff  (getCoeff         )--> SendToken;
		GetCoeff  (getCoeff_is64x64 )--> GetCoeff;
		SendToken (sendToken.skip   )--> GetCoeff;
		SendToken (sendToken.launch )--> SendBlock;

		SendBlock (sendBlock.launch )--> SendBlock;
		SendBlock (sendBlock.done   )--> GetCoeff;
	end
end
